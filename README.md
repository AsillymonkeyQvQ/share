# share

```SQL
use sybsystemprocs
go
if object_id('sp_f_group_concat') is not null
    drop function sp_f_group_concat
go
create function sp_f_group_concat
(@sql       varchar(1000)
,@separator varchar(10) = NULL
)
returns varchar(1000)
as
/*
    sp_f_group_concat

    ASE implementation of MySQL's GROUP_CONCAT() function.

    Requirements/Assumptions
    ========================
    - ASE version must support a) user defined functions and b) table variables
    - @sql is a standalone query that generates a result set consisting of a single varchar column
    - @sql includes an 'order by' clause if needed (ie, this function does not attempt to order the results generated by @sql)

*/
    set nocount on

    declare @string     varchar(100),
        @string_list    varchar(1000)

    -- default delimiter to ',' if not supplied

    select  @separator = isnull(@separator,',')

    -- create/populate @strings table

    declare @strings table (string varchar(100))

    exec("insert @strings " + @sql)

    -- assume TF:7703 is not enabled, so can't use a single SELECT to append to a @variable
    -- assume @sql has a 'order by' clause and that our cursor will pull from @strings in the same order

    declare string_cur cursor
    for
    select  string
    from    @strings
    for read only

    -- loop through @strings rows, appending individual strings to @string_list

    open string_cur

    fetch string_cur into @string

    while @@sqlstatus = 0
    begin
        select  @string_list = @string_list + case when @string_list is not NULL then @separator end + @string
        fetch string_cur into @string
    end

    close string_cur
    deallocate cursor string_cur

    -- send concatenated list of strings back to calling process
 
    return @string_list
go
grant execute on sp_f_group_concat to public
go
```
